MODULE FXTRAN_ACDC_GEMV_MOD_SINGLEBLOCK

!
! Copyright 2025 Meteo-France
! All rights reserved
! philippe.marguinaud@meteo.fr
!

#if defined(_OPENACC) && (__NVCOMPILER==1)
USE CUBLAS
#endif

IMPLICIT NONE

PRIVATE

PUBLIC :: FXTRAN_ACDC_GEMV_SINGLEBLOCK

CONTAINS

SUBROUTINE FXTRAN_ACDC_GEMV_SINGLEBLOCK (KIDIA, KFDIA, TRANSA, TRANSB, M, N, K, ALPHA, A, &
                                       & LDA, B, LDB, BETA, C, LDC, LDDONE, LDACC)

USE FXTRAN_ACDC_ABORT_MOD

                                ! VERINTS
INTEGER     :: KIDIA
INTEGER     :: KFDIA
CHARACTER*1 :: TRANSA           ! 'N'
CHARACTER*1 :: TRANSB           ! 'T'
INTEGER     :: M                ! KPROMA
INTEGER     :: N                ! 1
INTEGER     :: K                ! KLEVIN
REAL*8      :: ALPHA            ! 1.0_JPRD
REAL*8      :: A (:, :)         ! ZIN
INTEGER     :: LDA              ! KPROMA
REAL*8      :: B (:, :)         ! PINTE
INTEGER     :: LDB              ! KLEVOUT
REAL*8      :: BETA             ! 0.0_JPRB
REAL*8      :: C (:)            ! POUTS
INTEGER     :: LDC              ! KPROMA
LOGICAL     :: LDDONE
LOGICAL     :: LDACC

INTEGER :: JM, JN, JK
CHARACTER*1, SAVE :: CLENV = ''
LOGICAL, SAVE :: LLSIMPLE_DGEMM = .FALSE.

IF (CLENV == '') THEN
  CALL GETENV ('FXTRAN_ACDC_SIMPLE_DGEMM', CLENV)
  LLSIMPLE_DGEMM = CLENV (1:1) == '1' 
  CLENV = '0'
ENDIF

IF (TRANSA /= 'N') CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (TRANSB /= 'T') CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')

CALL CHECKCONTIGUOUS2 (A)
CALL CHECKCONTIGUOUS2 (B)
CALL CHECKCONTIGUOUS1 (C)

IF (LDA /= SIZE (A, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (LDB /= SIZE (B, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (LDC /= SIZE (C, 1)) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (KIDIA /= 1) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (ALPHA /= 1._8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
IF (BETA /= 0._8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')

IF (LLSIMPLE_DGEMM) THEN

  !$ACC PARALLEL LOOP GANG VECTOR &
  !$ACC&PRESENT (A, B, C) &
  !$ACC&PRIVATE (JK, JN, JM) IF(LDACC) 

  DO JM = KIDIA, KFDIA
    C (JM) = 0.
    DO JK = 1, K
      C (JM) = C (JM) + B (LDB , JK) * A (JM, JK)
    ENDDO
  ENDDO

ELSE

  IF (LDACC) THEN
#if defined(_OPENACC) && (__NVCOMPILER==1)

    !$ACC PARALLEL LOOP GANG VECTOR &
    !$ACC&PRESENT (A, B, C) &
    !$ACC&PRIVATE (JK, JN, JM)  
  
    DO JM = KIDIA, KFDIA
      C (JM) = 0.
      DO JK = 1, K
        C (JM) = C (JM) + B (LDB , JK) * A (JM , JK)
      ENDDO
    ENDDO

#else
    CALL DGEMM ('N','T', M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC)
#endif

  ELSE
    CALL DGEMM ('N','T', M, N, K, ALPHA, A(1,1), LDA, B(LDB,1), LDB, BETA, C(1), LDC)
  ENDIF

ENDIF

LDDONE = .TRUE.

END SUBROUTINE

SUBROUTINE CHECKCONTIGUOUS1 (P)

USE FXTRAN_ACDC_ABORT_MOD

REAL*8 :: P (:)
IF (LOC (P (2)) - LOC (P (1)) /= 8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END SUBROUTINE

SUBROUTINE CHECKCONTIGUOUS2 (P)

USE FXTRAN_ACDC_ABORT_MOD

REAL*8 :: P (:, :)
IF (LOC (P (1, 2)) - LOC (P (1, 1)) /= SIZE (P, 1) * 8) CALL FXTRAN_ACDC_ABORT ('FXTRAN_ACDC_GEMM')
END SUBROUTINE

END MODULE
