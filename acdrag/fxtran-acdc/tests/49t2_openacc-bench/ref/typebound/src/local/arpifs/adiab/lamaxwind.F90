SUBROUTINE LAMAXWIND (YDML_DYN, YDCST, KPROMA, KFLEVG, KST, KEND, PURL, PVRL, PRCOLON, PGEMU)

!$ACDC singlecolumn  --process-pointers


!**** *LAMAXWIND*  - Check wind maximum values

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 27-02-2024

USE PARKIND1           , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK            , ONLY : JPHOOK, LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULERR
USE YOMCST             , ONLY : TCST
USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE

IMPLICIT NONE

#include "abor1.intfb.h"

TYPE(MODEL_DYNAMICS_TYPE) ,INTENT(IN) :: YDML_DYN
TYPE(TCST)                ,INTENT(IN) :: YDCST
INTEGER(KIND=JPIM)        ,INTENT(IN) :: KFLEVG
INTEGER(KIND=JPIM)        ,INTENT(IN) :: KPROMA
INTEGER(KIND=JPIM)        ,INTENT(IN) :: KST
INTEGER(KIND=JPIM)        ,INTENT(IN) :: KEND
REAL(KIND=JPRB)           ,INTENT(IN) :: PURL(KPROMA,KFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN) :: PVRL(KPROMA,KFLEVG)
REAL(KIND=JPRD)           ,INTENT(IN) :: PRCOLON(KPROMA)
REAL(KIND=JPRB)           ,INTENT(IN) :: PGEMU(KPROMA)

REAL(KIND=JPRB) :: ZEW, ZEWX
REAL(KIND=JPRB) :: ZNOR, ZNORX
REAL(KIND=JPRB) :: ZVMAX1, ZVMAX2
INTEGER(KIND=JPIM) :: JLEV
INTEGER(KIND=JPIM) :: JROF
INTEGER(KIND=JPIM) :: ILEVEXP
INTEGER(KIND=JPIM) :: IROFEXP

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LAMAXWIND',0,ZHOOK_HANDLE)

ASSOCIATE(YDDYN=>YDML_DYN%YRDYN)

ASSOCIATE(VMAX1=>YDDYN%VMAX1, VMAX2=>YDDYN%VMAX2)

ZNOR=0.0_JPRB
ZEW=0.0_JPRB
ZVMAX1=VMAX1*VMAX1
ZVMAX2=VMAX2*VMAX2

DO JLEV=1,KFLEVG
  DO JROF=KST,KEND
    ZNOR=MAX(PVRL(JROF,JLEV)*PVRL(JROF,JLEV),ZNOR)
    ZEW =MAX(PURL(JROF,JLEV)*PURL(JROF,JLEV),ZEW )
  ENDDO
ENDDO

IF (ZNOR > ZVMAX1) THEN
  WRITE(NULERR,*) ' MAX V WIND=',SQRT(ZNOR)
ENDIF
IF (ZEW > ZVMAX1) THEN
  WRITE(NULERR,*) ' MAX U WIND=',SQRT(ZEW)
ENDIF
IF (ZNOR > ZVMAX2) THEN
  ZNOR=0.0_JPRB
  DO JLEV=1,KFLEVG
    DO JROF=KST,KEND
      ZNORX=PVRL(JROF,JLEV)*PVRL(JROF,JLEV)
      ZNOR=MAX(ZNORX,ZNOR)
      IF(ZNOR == ZNORX) THEN
        ILEVEXP=JLEV
        IROFEXP=JROF
      ENDIF
    ENDDO
  ENDDO
  WRITE(NULERR,*) ' V WIND =',SQRT(ZNOR),' IS TOO STRONG, EXPLOSION.'
  WRITE(NULERR,*) ' LEVEL= ',ILEVEXP,' POINT= ',IROFEXP
  WRITE(NULERR,*) ' LON  = ',ACOS(PRCOLON(IROFEXP))*180._JPRB/YDCST%RPI,' degrees'
  WRITE(NULERR,*) ' LAT  = ',ASIN(PGEMU(IROFEXP))*180._JPRB/YDCST%RPI,' degrees'
  CALL ABOR1(' !V WIND TOO STRONG, EXPLOSION!!!')
ENDIF
IF (ZEW > ZVMAX2) THEN
  ZEW=0.0_JPRB
  DO JLEV=1,KFLEVG
    DO JROF=KST,KEND
      ZEWX=PURL(JROF,JLEV)*PURL(JROF,JLEV)
      ZEW=MAX(ZEWX,ZEW)
      IF(ZEW == ZEWX) THEN
        ILEVEXP=JLEV
        IROFEXP=JROF
      ENDIF
    ENDDO
  ENDDO
  WRITE(NULERR,*) ' U WIND =',SQRT(ZEW),' IS TOO STRONG, EXPLOSION.'
  WRITE(NULERR,*) ' LEVEL= ',ILEVEXP,' POINT= ',IROFEXP
  WRITE(NULERR,*) ' LON  = ',ACOS(PRCOLON(IROFEXP))*180._JPRB/YDCST%RPI,' degrees'
  WRITE(NULERR,*) ' LAT  = ',ASIN(PGEMU(IROFEXP))*180._JPRB/YDCST%RPI,' degrees'
  CALL ABOR1(' !U WIND TOO STRONG, EXPLOSION!!!')
ENDIF

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('LAMAXWIND',1,ZHOOK_HANDLE)

END SUBROUTINE LAMAXWIND 

