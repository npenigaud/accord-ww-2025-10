MODULE YOERADGHG

!$ACDC methods 


!     -------------------------------------------------------------------
!     *YOERADGHG* - STORAGE OF GAS CLIMATOLOGY INTERPOLATED TO MODEL TIME
!     -------------------------------------------------------------------
! Modifications
!   2019-01-25  R. Hogan  Completely rewrote

USE PARKIND1  ,ONLY : JPRB, JPIM

IMPLICIT NONE

! Type to store greenhouse-gas climatology interpolated to model time
TYPE :: TRADGHG
  ! Sine of latitude where monthly mean climatologies are provided
  REAL(KIND=JPRB), ALLOCATABLE :: SINLAT(:)
  ! Pressure (Pa) at half levels *indexed from 0* up to the number of layers
  REAL(KIND=JPRB), ALLOCATABLE :: PRESSURE_HL(:)
  ! Layer-integrated gas mass concentrations (Pa, i.e. mass mixing
  ! ratio multiplied by pressure difference across the layer),
  ! dimensioned (NLATITUDE,NPRESSURE), having already been
  ! interpolated in time
  REAL(KIND=JPRB), ALLOCATABLE :: MASS_CH4(:,:),  MASS_CO2(:,:),    MASS_N2O(:,:)
  REAL(KIND=JPRB), ALLOCATABLE :: MASS_NO2(:,:),  MASS_CFC11(:,:),  MASS_CFC12(:,:)
  REAL(KIND=JPRB), ALLOCATABLE :: MASS_CCL4(:,:), MASS_HCFC22(:,:), MASS_O3(:,:)

  ! Number of pressures and latitudes
  INTEGER(KIND=JPIM) :: NLATITUDE = 0, NPRESSURE = 0

CONTAINS

  PROCEDURE :: INIT => INIT_RAD_GHG

PROCEDURE :: ACDC_COPY => ACDC_COPY_TRADGHG
PROCEDURE :: ACDC_CRC64 => ACDC_CRC64_TRADGHG
PROCEDURE :: ACDC_HOST => ACDC_HOST_TRADGHG
PROCEDURE :: ACDC_LOAD => ACDC_LOAD_TRADGHG
PROCEDURE :: ACDC_SAVE => ACDC_SAVE_TRADGHG
PROCEDURE :: ACDC_SIZE => ACDC_SIZE_TRADGHG
PROCEDURE :: ACDC_WIPE => ACDC_WIPE_TRADGHG
END TYPE TRADGHG

TYPE(TRADGHG), POINTER :: YRADGHG => NULL()

INTERFACE

MODULE SUBROUTINE ACDC_COPY_TRADGHG (SELF, LDCREATED, LDFIELDAPI)

IMPLICIT NONE
CLASS (TRADGHG), INTENT (IN), TARGET :: SELF
LOGICAL, OPTIONAL, INTENT (IN) :: LDCREATED, LDFIELDAPI
END SUBROUTINE

MODULE SUBROUTINE ACDC_CRC64_TRADGHG (SELF, KLUN, CDPATH)
USE FXTRAN_ACDC_CRC64_INTRINSIC_MOD, ONLY : FCRC64 => FXTRAN_ACDC_CRC64
IMPLICIT NONE
CLASS (TRADGHG), TARGET :: SELF
INTEGER, INTENT (IN) :: KLUN
CHARACTER(LEN=*), INTENT (IN) :: CDPATH
END SUBROUTINE

MODULE SUBROUTINE ACDC_HOST_TRADGHG (SELF)

IMPLICIT NONE
CLASS (TRADGHG), TARGET :: SELF
END SUBROUTINE

MODULE SUBROUTINE ACDC_LOAD_TRADGHG (SELF, KLUN)
USE PARKIND1, ONLY : JPRD
IMPLICIT NONE
CLASS (TRADGHG), INTENT (OUT), TARGET :: SELF
INTEGER, INTENT (IN) :: KLUN
END SUBROUTINE

MODULE SUBROUTINE ACDC_SAVE_TRADGHG (SELF, KLUN)

IMPLICIT NONE
CLASS (TRADGHG), INTENT (IN), TARGET :: SELF
INTEGER, INTENT (IN) :: KLUN
END SUBROUTINE

MODULE FUNCTION ACDC_SIZE_TRADGHG (SELF, CDPATH, LDPRINT) RESULT (KSIZE)

IMPLICIT NONE
CLASS (TRADGHG),     INTENT (IN), TARGET :: SELF
CHARACTER(LEN=*), INTENT (IN), OPTIONAL :: CDPATH
LOGICAL,          INTENT (IN), OPTIONAL :: LDPRINT
INTEGER*8 :: KSIZE
END FUNCTION

MODULE SUBROUTINE ACDC_WIPE_TRADGHG (SELF, LDDELETED, LDFIELDAPI)

IMPLICIT NONE
CLASS (TRADGHG), INTENT (IN), TARGET :: SELF
LOGICAL, OPTIONAL, INTENT (IN) :: LDDELETED, LDFIELDAPI
END SUBROUTINE


END INTERFACE

CONTAINS

!-----------------------------------------------------------------------
! Initialize pressure and latitude, and allocate space for gases
SUBROUTINE INIT_RAD_GHG(SELF, PLATITUDE, PPRESSURE)

  USE YOMHOOK   ,ONLY : LHOOK, DR_HOOK, JPHOOK
  USE YOMCST    ,ONLY : RPI

  CLASS(TRADGHG),  INTENT(INOUT) :: SELF
  REAL(KIND=JPRB), INTENT(IN)    :: PLATITUDE(:), PPRESSURE(:)

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  IF (LHOOK) CALL DR_HOOK('YOERADGHG:INIT_RAD_GHG',0,ZHOOK_HANDLE)

  ! We only do anything if the type has not already been initialized
  IF (SELF%NLATITUDE <= 0) THEN
    SELF%NLATITUDE = SIZE(PLATITUDE)
    SELF%NPRESSURE = SIZE(PPRESSURE)
    ALLOCATE(SELF%SINLAT    (SELF%NLATITUDE))
    ALLOCATE(SELF%PRESSURE_HL(0:SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_CH4   (SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_CO2   (SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_N2O   (SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_NO2   (SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_CFC11 (SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_CFC12 (SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_CCL4  (SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_HCFC22(SELF%NLATITUDE,SELF%NPRESSURE))
    ALLOCATE(SELF%MASS_O3    (SELF%NLATITUDE,SELF%NPRESSURE))

    ! Initialize sine of latitude
    SELF%SINLAT = SIN(PLATITUDE * (RPI/180.0_JPRB))

    ! Initialize pressure at half levels
    SELF%PRESSURE_HL(0) = 0.0_JPRB
    SELF%PRESSURE_HL(1:SELF%NPRESSURE-1) = 0.5_JPRB * (PPRESSURE(1:SELF%NPRESSURE-1) + PPRESSURE(2:SELF%NPRESSURE))
    SELF%PRESSURE_HL(SELF%NPRESSURE) = MAX(110000.0_JPRB, 1.5_JPRB*PPRESSURE(SELF%NPRESSURE) &
         &                                               -0.5_JPRB*PPRESSURE(SELF%NPRESSURE-1))
  ENDIF

  IF (LHOOK) CALL DR_HOOK('YOERADGHG:INIT_RAD_GHG',1,ZHOOK_HANDLE)

END SUBROUTINE INIT_RAD_GHG

END MODULE YOERADGHG


