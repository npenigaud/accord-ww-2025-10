SUBROUTINE ACPROD (YDCST, KIDIA, KFDIA, KLON, KLEV, PR, PCP, PT, PQ, PU, PV, PAPRSF, &
                  & PAPHIF, PDIFCQ, PDIFCS, PSTRCU, PSTRCV, PPRODTH)

!$ACDC singlecolumn 



!**** *ACPROD * - COMPUTE TKE PRODUCTION FROM DEEP CONVECTION

!     Sujet.
!     ------
!     - COMPUTE TKE PRODUCTION FROM DEEP CONVECTION

!**   Interface.
!     ----------
!        *CALL* *ACPROD*

!-----------------------------------------------------------------------

!     Auteur.
!     -------
!      03-18, Yves Bouteloup
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!   -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT.
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - 2D (0:KLEV) .

! PDIFCQ     : Deep convection flux of Qv
! PDIFCS     : Deep convection flux of CpT

! - 2D (1:KLEV) .

! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PCP        : CHALEUR MASSIQUE
! PT         : TEMPERATURE (APRES AJUSTEMENT CONVECTIF).
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.

!-----------------------------------------------------------------------

! -   ARGUMENTS EN ENTREE/SORTIE.
!     ---------------------------

! - 2D (0:KLEV) .

! PPRODTH_CVP  : Thermal TKE production from deep convection

!-----------------------------------------------------------------------

USE PARKIND1  , ONLY : JPIM   ,JPRB
USE YOMHOOK   , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMCST    , ONLY : TCST

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TCST)        ,INTENT(IN)    :: YDCST
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFCQ(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFCS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRCU(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRCV(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PPRODTH(KLON,KLEV) 

!-----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV, JLON
REAL(KIND=JPRB)    :: ZRHOF(KLON,KLEV),ZCPH(KLON),ZQH(KLON),ZTH(KLON)
REAL(KIND=JPRB)    :: ZTHETAH(KLON),ZRHOH(KLON),ZEXNERF(KLON,KLEV),ZEXNERH(KLON)
REAL(KIND=JPRB)    :: ZWPTP(KLON), ZWPQP(KLON), ZWPTHEPV(KLON)
REAL(KIND=JPRB)    :: ZDUODPHI(KLON), ZDVODPHI(KLON), ZPRODDYN(KLON)
REAL(KIND=JPHOOK)  :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('ACPROD',0,ZHOOK_HANDLE)

ASSOCIATE(RG=>YDCST%RG, RKAPPA=>YDCST%RKAPPA, RATM =>YDCST%RATM)

! Preliminary 

DO JLEV=1,KLEV
   DO JLON=KIDIA,KFDIA
     ZEXNERF(JLON,JLEV) = (RATM/PAPRSF(JLON,JLEV))**RKAPPA
     ZRHOF  (JLON,JLEV) = PAPRSF(JLON,JLEV)/(PT(JLON,JLEV)*PR(JLON,JLEV))
   ENDDO
ENDDO   

! Main computation
DO JLEV=1,KLEV-1
   DO JLON=KIDIA,KFDIA
     ZEXNERH (JLON)  = 0.5_JPRB*(ZEXNERF(JLON,JLEV)+ZEXNERF(JLON,JLEV+1))
     ZRHOH   (JLON)  = 0.5_JPRB*(ZRHOF(JLON,JLEV)+ZRHOF(JLON,JLEV+1))
     ZCPH    (JLON)  = 0.5_JPRB*(PCP(JLON,JLEV)+PCP(JLON,JLEV+1))
     ZQH     (JLON)  = 0.5_JPRB*(PQ(JLON,JLEV)+PQ(JLON,JLEV+1))
     ZTH     (JLON)  = 0.5_JPRB*(PT(JLON,JLEV)+PT(JLON,JLEV+1))
     ZTHETAH (JLON)  = ZEXNERH(JLON)*ZTH(JLON)
     ZWPTP   (JLON)  = PDIFCS(JLON,JLEV)/ZRHOH(JLON)/ZCPH(JLON)
     ZWPQP   (JLON)  = PDIFCQ(JLON,JLEV)/ZRHOH(JLON)
     ZDUODPHI(JLON)  = (PU(JLON,JLEV)-PU(JLON,JLEV+1))/(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))  
     ZDVODPHI(JLON)  = (PV(JLON,JLEV)-PV(JLON,JLEV+1))/(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
     ZPRODDYN(JLON)  = -RG*(PSTRCU(JLON,JLEV)*ZDUODPHI(JLON) + PSTRCV(JLON,JLEV)*ZDVODPHI(JLON))/ZRHOH(JLON)
     ZWPTHEPV (JLON)        = ZEXNERH(JLON)*((1._JPRB+0.608_JPRB*ZQH(JLON))*ZWPTP(JLON) + 0.608_JPRB*ZTH(JLON)*ZWPQP(JLON))
     PPRODTH(JLON,JLEV) = PPRODTH(JLON,JLEV) + (RG/ZTHETAH(JLON))*ZWPTHEPV(JLON) + ZPRODDYN(JLON)
   ENDDO
ENDDO   


!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACPROD',1,ZHOOK_HANDLE)
END SUBROUTINE ACPROD
