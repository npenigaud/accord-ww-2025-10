SUBROUTINE CPTENDS ( YDCST, YDML_PHY_MF,KLON, KIDIA, KFDIA, KFLEV, KCSS, PDT,&
 !-------------------------------------------------------------------------------
 ! - INPUT 2D .
 & PFPLCL, PFPLSL, PFPLCN, PFPLSN,&
 & PFRSO, PFRTH,&
 ! - INPUT 1D .
 & PALBNS1 ,PCT, PC1, PC2,&
 & PFCHSP, PFCLL, PFCLN, PFCS, PFEVI, PFEVL, PFEVN,&
 & PFEVV , PFGEL, PFGELS, PFLWSP, PFONTE, PFTR,&
 & PLSM, PRHONS1, PRUISL, PRUISP, PRUISS, PSNS1, PVEG,&
 ! - OUTPUT 1D .
 & PTDTS, PTDTP, PTDWS, PTDWSI, PTDWP, PTDWPI, PTDWL, PTDSNS,&
 & PTDALBNS, PTDRHONS )  

!$ACDC singlecolumn 

!****
!     ------------------------------------------------------------------

!     ACTION DES PROCESSUS PHYSIQUES SUR LES CHAMPS AU SOL
!     INTERFACE AVEC LES PARAMETRISATIONS PHYSIQUES  (IALPP)
!     ------------------------------------------------------------------

!     BUT
!     ---
!      CALCUL DES TENDANCES PHYSIQUES
!      SUR - LA TEMPERATURE DU SOL
!          - LA TEMPERATURE PROFONDE
!          - LE RESERVOIR SUPERFICIEL
!          - LE RESERVOIR SUPERFICIEL DE GLACE ( DANS LE CAS LSOLV )
!          - LE RESERVOIR PROFOND
!          - LE RESERVOIR PROFOND DE GLACE ( DANS LE CAS LSOLV )
!          - LE RESERVOIR D'INTERCEPTION ( DANS LE CAS LSOLV )
!          - LE RESERVOIR DE NEIGE
!          - L'ALBEDO DE LA NEIGE
!          - LA DENSITE DE LA NEIGE

!     ARGUMENTS D ENTREE
!     ------------------
!       KLON   : DIMENSION HORIZONTALE
!       KIDIA  : BORNE INITIALE HORIZONTALE DES CALCULS
!       KFDIA : BORNE FINALE HORIZONTALE DES CALCULS
!       KFLEV : DIMENSION ET BORNE VERTICALE
!       KCSS  : NBRE DE COUCHES DANS LE SOL
!       PDT   : PAS DE TEMPS EFFECTIF

! --- INPUT 2D
!     --------
!       PFPLCL, PFPLSL, PFPLCN, PFPLSN (KLON,0:KFLEV): FLUX DE PRECIPITATIONS
!       PFRSO, PFRTH (IDEM): FLUX DE RAYONNEMENT

! --- INPUT 1D
!     --------
!       PALBNS1(KLON): ALBEDO DE LA NEIGE
!       PC1: COEFF. HYDRIQUE REPRESENTANT L'INTENSITE AVEC LAQUELLE
!               LES FLUX DE SURFACE PARTICIPENT A L'EVOLUTION DE WS (LSOLV).
!       PC2   : COEFF. HYDRIQUE TRADUISANT LA RAPIDITE DES TRANSFERTS D'EAU
!               ENTRE LES DEUX RESERVOIRS (LSOLV).
!       PCT   : COEFFICIENT THERMIQUE DU MILIEU SOL-VEGETATION.
!       PFCHSP (IDEM): FLUX DE CHALEUR DANS LE SOL
!       PFCLL, PFCLN (IDEM): FLUX DE CHALEUR LATENTE
!       PFCS  : FLUX DE CHALEUR SENSIBLE
!                     (DANS L HYPOTHSE OU PS NE VARIE PAS)
!       PFEVI,PFEVL,PFEVN : EVAPORATION DES RESERVOIRS DE GLACE, EAU ET
!                           NEIGE (Y COMPRIS PFEVI).
!       PFEVV  : FLUX D'EVAPOTRANSPIRATION (LSOLV).
!       PFGEL  : FLUX DE GEL DE L EAU DU SOL (LSOLV et LFGEL).
!       PFGELS : FLUX DE GEL DE L EAU DE SURFACE (LSOLV et LFGELS).
!       PFLWSP (IDEM): FLUX D EAU DANS LE SOL
!       PFONTE (KLON): FLUX DE FONTE DE NEIGE
!       PFTR   : FLUX DE TRANSPIRATION (LSOLV).
!       PLSM (IDEM): INDICATEUR TERRE(1.)/MER(0.)
!       PRHONS1 (IDEM) : DENSITE DE LA NEIGE
!       PRUISL (IDEM):RUISSELLEMENT DU RESERVOIR D EAU SUPERFICIEL (LSOLV).
!       PRUISS, PRUISP (IDEM) : RUISSELLEMENT DES RESERVOIRS D EAU.
!       PSNS1  : RESERVOIR DE NEIGE.
!       PVEG   : PROPORTION DE SOL COUVERT PAR LA VEGETATION.

!     ARGUMENTS IMPLICITES
!     --------------------
!       CONSTANTES UNIVERSELLES =  COMMON /YOMCST/ : RDAY,RLMLT
!       PROPRIETES DU SOL       =  COMMON /YOMPHY1/: RTINER
!       LOGIQUES DE LA PHYSIQUE =  COMMON /YOMPHY/ : LSOLV,LNEIGE,LFGEL,
!                                                    LFGELS

!     SORTIES
!     -------
!          PTDTS(KLON) : TENDANCE PHYSIQUE SUR LA TEMPERATURE DE SURFACE
!          PTDTP(IDEM,KCSS) :    "     "   SUR LA TEMPERATURE PROFONDE
!          PTDWS(IDEM) :    "     "   SUR LE RESERVOIR D'EAU SUPERFICIEL
!          PTDWP(IDEM) :    "     "   SUR LE RESERVOIR D'EAU PROFOND
!          PTDWPI(IDEM):    "     "   SUR LE RESERVOIR D'EAU PROFOND GELE
!          PTDWSI(IDEM):    "     "   SUR LE RESERVOIR D'EAU GELEE DE SURFACE
!          PTDWL(IDEM) :    "     "   SUR LE RESERVOIR D'EAU D'INTERCEPTION
!                                         SI LSOLV
!          PTDSNS(IDEM):    "     "   SUR LE RESERVOIR DE NEIGE (LNEIGE)
!          PTDALBNS(IDEM):  "     "   SUR L'ALBEDO DE LA NEIGE
!          PTDRHONS(IDEM):  "     "   SUR LA DENSITE DE LA NEIGE

!     AUTEUR:
!     ------- ERIC BAZILE  92/10 INSPIRE PAR J.NOILHAN ET A.JOLY
!             MODIFIE 94/10 PAR M. DEQUE (MODELE 4 TEMPERATURES SOL)

!     MODIFICATIONS:
!     --------------
!      02-09 Schema de neige LVGSN. - E. Bazile.
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!     R. El Khatib 22-Jun-2022 A contribution to simplify phasing after the refactoring of YOMCLI/YOMCST/YOETHF.
!     ------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD, ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1            , ONLY : JPIM, JPRB
USE YOMHOOK             , ONLY : DR_HOOK, JPHOOK, LHOOK

USE YOMCST              , ONLY : TCST

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(TCST)                  ,INTENT(IN)   :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE) ,INTENT(IN)   :: YDML_PHY_MF
INTEGER(KIND=JPIM)          ,INTENT(IN)   :: KLON 
INTEGER(KIND=JPIM)          ,INTENT(IN)   :: KIDIA 
INTEGER(KIND=JPIM)          ,INTENT(IN)   :: KFDIA 
INTEGER(KIND=JPIM)          ,INTENT(IN)   :: KFLEV 
INTEGER(KIND=JPIM)          ,INTENT(IN)   :: KCSS 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PDT 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFPLCL(KLON,0:KFLEV) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFPLSL(KLON,0:KFLEV) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFPLCN(KLON,0:KFLEV) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFPLSN(KLON,0:KFLEV) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFRSO(KLON,0:KFLEV) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFRTH(KLON,0:KFLEV) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PALBNS1(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PCT(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PC1(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PC2(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFCHSP(KLON,KCSS) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFCLL(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFCLN(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFCS(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFEVI(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFEVL(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFEVN(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFEVV(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFGEL(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFGELS(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFLWSP(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFONTE(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PFTR(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PLSM(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PRHONS1(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PRUISL(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PRUISP(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PRUISS(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PSNS1(KLON) 
REAL(KIND=JPRB)             ,INTENT(IN)   :: PVEG(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDTS(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDTP(KLON,KCSS) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDWS(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDWSI(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDWP(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDWPI(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDWL(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDSNS(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDALBNS(KLON) 
REAL(KIND=JPRB)             ,INTENT(OUT)  :: PTDRHONS(KLON) 

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JCSS, JLON

REAL(KIND=JPRB) :: ZBFLO, ZCI, ZEPSN, ZFGEL, ZITS, ZPRECN, ZTO3, ZVEG
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CPTENDS',0,ZHOOK_HANDLE)
ASSOCIATE(RDAY=>YDCST%RDAY, RLMLT=>YDCST%RLMLT, USUPRC=>YDML_PHY_MF%YRPHY0%USUPRC, &
 & SODELX=>YDML_PHY_MF%YRPHY1%SODELX, TOLIN=>YDML_PHY_MF%YRPHY1%TOLIN, RTINER=>YDML_PHY_MF%YRPHY1%RTINER, &
 & TOEXP=>YDML_PHY_MF%YRPHY1%TOEXP, ALBMIN=>YDML_PHY_MF%YRPHY1%ALBMIN, RHOMIN=>YDML_PHY_MF%YRPHY1%RHOMIN, &
 & RHOMAX=>YDML_PHY_MF%YRPHY1%RHOMAX, WNEW=>YDML_PHY_MF%YRPHY1%WNEW, &
 & LSOLV=>YDML_PHY_MF%YRPHY%LSOLV, LVGSN=>YDML_PHY_MF%YRPHY%LVGSN, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, &
 & LSNV=>YDML_PHY_MF%YRPHY%LSNV, LFGEL=>YDML_PHY_MF%YRPHY%LFGEL)
!     ------------------------------------------------------------------

!     ------------------------------------------------------------------
!     1.- CALCUL DES TENDANCES PHYSIQUES

ZEPSN=1.E-3_JPRB
IF (LSOLV) THEN

!        CAS LSOLV ROUTINE ACDROV ACTIVE

!     ... DE LA TEMPERATURE PROFONDE :
!     -------------------------------

  DO JCSS=1,KCSS-1
    DO JLON = KIDIA, KFDIA
      PTDTP(JLON,JCSS) = PLSM(JLON) * (SODELX(0)/SODELX(JCSS))&
       & *PCT(JLON)*(PFCHSP(JLON,JCSS)-PFCHSP(JLON,JCSS+1))  
    ENDDO
  ENDDO
  DO JLON = KIDIA, KFDIA
    PTDTP(JLON,KCSS) = PLSM(JLON) * (SODELX(0)/SODELX(KCSS))&
     & *PCT(JLON)*PFCHSP(JLON,KCSS)  
    IF (LFGEL) THEN
      PTDTP(JLON,1) = PTDTP(JLON,1) + PLSM(JLON) *&
       & PCT(JLON)*RLMLT*PFGEL(JLON)  
    ENDIF
  ENDDO
  DO JLON = KIDIA, KFDIA

    ZITS = PLSM(JLON)/RDAY

!     ... DE LA TEMPERATURE DE SURFACE :
!     ---------------------------------
    IF (LFGEL) THEN
      ZFGEL=PFGELS(JLON)
    ELSE
      ZFGEL=PFGEL (JLON)
    ENDIF
    PTDTS(JLON) = PLSM(JLON) * PCT(JLON) * ( PFRSO(JLON,KFLEV) +&
     & PFRTH(JLON,KFLEV) + PFCS(JLON) + PFCLL(JLON) + PFCLN(JLON) -&
     & PFCHSP(JLON,1) - RLMLT * ( PFONTE(JLON)-ZFGEL ) )  

!     ... DU RESERVOIR D'INTERCEPTION:
!     --------------------------------

    ZVEG=PVEG(JLON)/(1.0_JPRB+USUPRC*PFPLCL(JLON,KFLEV))
    PTDWL(JLON) = PLSM(JLON) * (&
     & ZVEG * (PFPLSL(JLON,KFLEV)+PFPLCL(JLON,KFLEV)) +&
     & PFEVV(JLON) - PFTR(JLON) - PRUISL(JLON) )  

!     ... DU RESERVOIR SUPERFICIEL LIQUIDE ET GLACE :
!     -----------------------------------------------

    ZBFLO=(1.0_JPRB-ZVEG)*(PFPLSL(JLON,KFLEV)+PFPLCL(JLON,KFLEV))&
     & +PRUISL(JLON)+PFONTE(JLON)+PFEVL(JLON)-PFEVV(JLON)  
    ZCI=PC2(JLON)*PDT*ZITS
    PTDWS(JLON) = PLSM(JLON) * (&
     & (PC1(JLON)*ZBFLO-PFLWSP(JLON))/(1.0_JPRB+ZCI) - PRUISS(JLON)&
     & - PFGELS(JLON) )  

    IF (LFGEL) THEN
      PTDWSI(JLON) = PLSM(JLON) * (PFGELS(JLON)+PFEVI(JLON))
    ENDIF

!     ... DU RESERVOIR PROFOND LIQUIDE ET GLACE:
!     -----------------------------------------

    PTDWP(JLON) = PLSM(JLON) *&
     & ( ZBFLO+PFTR(JLON)-PRUISP(JLON)-PFGEL(JLON)-PFGELS(JLON))  

    IF (LFGEL) THEN
      PTDWPI(JLON) = PLSM(JLON) * (PFGEL(JLON)+PFGELS(JLON)+PFEVI(JLON))
    ENDIF

  ENDDO
ELSE

!        ROUTINE ACDRO ACTIVE

  DO JCSS=2,KCSS
    DO JLON = KIDIA, KFDIA
      PTDTP(JLON,JCSS) = 0
    ENDDO
  ENDDO

  DO JLON = KIDIA, KFDIA

!     ... DE LA TEMPERATURE DE SURFACE :
!     ---------------------------------

    PTDTS(JLON) = PLSM(JLON) * PCT(JLON) * ( PFRSO(JLON,KFLEV) +&
     & PFRTH(JLON,KFLEV) + PFCS(JLON) + PFCLL(JLON) + PFCLN(JLON) -&
     & PFCHSP(JLON,1) - RLMLT * PFONTE(JLON) )  

!     ... DE LA TEMPERATURE PROFONDE :
!     -------------------------------

    PTDTP(JLON,1) = PLSM(JLON) * (PCT(JLON)/RTINER) *PFCHSP(JLON,1)

!     ... DU RESERVOIR SUPERFICIEL :
!     -----------------------------

    PTDWS(JLON) = PLSM(JLON) * (&
     & PFPLSL(JLON,KFLEV) + PFPLCL(JLON,KFLEV) + PFONTE(JLON) -&
     & PRUISS(JLON) + PFEVL(JLON) - PFLWSP(JLON) )  

!     ... DU RESERVOIR PROFOND :
!     -------------------------

    PTDWP(JLON) = PLSM(JLON) * ( PFLWSP(JLON) - PRUISP(JLON) )

  ENDDO

ENDIF

!     ... ET DES VARIABLES NEIGE EVENTUELLES:
!     --------------------------------------

IF ( LNEIGE ) THEN
  DO JLON = KIDIA, KFDIA
    PTDSNS(JLON) = PLSM(JLON) * ( PFPLCN(JLON,KFLEV) +&
     & PFPLSN(JLON,KFLEV) - PFONTE(JLON)+PFEVN(JLON)-PFEVI(JLON))  
    IF (LSNV.OR.LVGSN) THEN
      ZPRECN=PLSM(JLON)*(PFPLCN(JLON,KFLEV)+PFPLSN(JLON,KFLEV))
      IF (PFONTE(JLON) > 0.0_JPRB) THEN
        PTDALBNS(JLON) = -TOEXP*(PALBNS1(JLON)-ALBMIN)+ZPRECN/WNEW
      ELSE
        PTDALBNS(JLON) = -TOLIN+ZPRECN/WNEW
      ENDIF
      ZTO3 = MIN(0.5_JPRB/PDT,ZPRECN/MAX(PSNS1(JLON),ZEPSN))
      PTDRHONS(JLON)= -TOEXP*(PRHONS1(JLON)-RHOMAX)&
       & -ZTO3*(PRHONS1(JLON)-RHOMIN)  
    ENDIF
  ENDDO
ENDIF

!     ------------------------------------------------------------------
!     REMARQUE IMPORTANTE
!     -------------------
!      RESTE A TRAITER LA QUESTION DES CONDITIONS AUX LIMITES INFERIEURES
!      DE L ADVECTION VERTICALE
!      CE CALCUL DEPEND DES VARIABLES PHYSIQUES AU SOL
!     ------------------------------------------------------------------

!     ------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPTENDS',1,ZHOOK_HANDLE)
END SUBROUTINE CPTENDS

