SUBROUTINE LATTEX_EXPL_VSPLTRANS (YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDML_DYN, YDCPG_SL1)

!$ACDC outline1    


USE PARKIND1              , ONLY : JPIM, JPRB
USE YOMHOOK               , ONLY : DR_HOOK, LHOOK, JPHOOK
USE GEOMETRY_MOD          , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD     , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MODEL_DYNAMICS_MOD    , ONLY : MODEL_DYNAMICS_TYPE
USE CPG_SL1_TYPE_MOD      , ONLY : CPG_SL1B_TYPE


IMPLICIT NONE

TYPE(GEOMETRY)                ,INTENT(IN)     :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE)           ,INTENT(IN)     :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE)           ,INTENT(IN)     :: YDCPG_OPTS
TYPE(MODEL_DYNAMICS_TYPE)     ,INTENT(IN)     :: YDML_DYN
TYPE(CPG_SL1B_TYPE)           ,INTENT(INOUT)  :: YDCPG_SL1

#include "vspltrans.intfb.h"

LOGICAL :: LLTDIABLIN
INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JROF
INTEGER (KIND=JPIM) :: JGFL

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LATTEX_EXPL_VSPLTRANS',0,ZHOOK_HANDLE)

DO JGFL = 1, SIZE (YDCPG_SL1%GFL)
  IF (YDCPG_SL1%GFL(JGFL)%YCOMP%LADV .AND. YDCPG_SL1%GFL(JGFL)%YCOMP%CSLINT=='LAITVSPCQM  ') THEN

    LLTDIABLIN = YDCPG_SL1%GFL(JGFL)%YCOMP%LTDIABLIN

    IF ((YDML_DYN%YRDYN%NSPLTHOI /= 0).OR.LLTDIABLIN) THEN

!$ACDC PARALLEL, TARGET=OpenMP {

      DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
        DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
          YDCPG_SL1%GFL(JGFL)%P_SPF(JROF,JLEV)=YDCPG_SL1%GFL(JGFL)%P_F(JROF,JLEV)
        ENDDO
      ENDDO
      CALL VSPLTRANS(YDML_DYN%YRSLINT%YRVSPLIP,YDGEOMETRY%YRDIM%NPROMA,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,&
                   & YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIMV%NFLSA,YDGEOMETRY%YRDIMV%NFLEN,YDCPG_SL1%GFL(JGFL)%P_SPF)

!$ACDC }

    ENDIF


!$ACDC PARALLEL, TARGET=OpenMP {

    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_SL1%GFL(JGFL)%P_SP(JROF,JLEV)=YDCPG_SL1%GFL(JGFL)%P(JROF,JLEV)
      ENDDO
    ENDDO
    CALL VSPLTRANS(YDML_DYN%YRSLINT%YRVSPLIP,YDGEOMETRY%YRDIM%NPROMA,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,&
                 & YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIMV%NFLSA,YDGEOMETRY%YRDIMV%NFLEN,YDCPG_SL1%GFL(JGFL)%P_SP)

!$ACDC }

  ENDIF
ENDDO

IF (LHOOK) CALL DR_HOOK('LATTEX_EXPL_VSPLTRANS',1,ZHOOK_HANDLE)

END SUBROUTINE LATTEX_EXPL_VSPLTRANS
