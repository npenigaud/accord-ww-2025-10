SUBROUTINE CPCFU(YDCST, YDCFU, YDRIP, YDCPG_OPTS, YDCPG_BNDS, YDMF_PHYS_OUT, YDCPG_MISC, YDCPG_DYN0, &
& YDVARS, YDMF_PHYS_SURF, PSTRCU, PSTRCV, PSTRDU, PSTRDV, PSTRTU, PSTRTV)

!$ACDC pointerparallel     


!**** *CPCFU* - CUMUL OF FLUX DIAGNOSTICS.

!     Purpose.
!     --------
!           DIAGNOSTICS OF PHYSICAL FLUXES.

!**   Interface.
!     ----------
!        *CALL* *CPCFU*

!        Explicit arguments :
!        --------------------

!       KPROMA                 - HORIZONTAL DIMENSION                 (INPUT)
!       KGL1,KGL2              - KGL1 TO KGL2 LATITUDES OF WORK.      (INPUT)
!       KSTABUF                - KSTABUF(JGL) FOR LATITUDE JGL        (INPUT)
!       FLUXES COMING FROM THE PHYSICAL PARAMETERIZATIONS             (INPUT)

!        --------------------

!     Method.
!     -------

!     Externals.
!     ----------

!     Reference.
!     ----------

!     Author.
!     -------
!      O. CLARY
!      Original : 06/92 D'APRES CUMCO

!     Modifications.
!     --------------
!      Modified : 01-08-07 R. El Khatib - pruning options
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!      Y.Seity       15-Jan-2007 add graupel and hail 
!      K. Yessad (July 2014): Move some variables.
!      R. Brozkova (Sep 2018): Added global normal irradiance.
!     ------------------------------------------------------------------

USE PARKIND1                , ONLY : JPIM, JPRB
USE YOMHOOK                 , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMRIP                  , ONLY : TRIP
USE YOMCFU                  , ONLY : TCFU
USE YOMCST                  , ONLY : TCST
USE CPG_OPTS_TYPE_MOD       , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE CPG_TYPE_MOD            , ONLY : CPG_DYN_TYPE, CPG_MISC_TYPE
USE MF_PHYS_TYPE_MOD        , ONLY : MF_PHYS_OUT_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD, ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD     , ONLY : FIELD_VARIABLES
USE TYPE_FLUXES             , ONLY : FLUXES_DESCRIPTOR


!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(TCST)              ,INTENT(IN)               :: YDCST
TYPE(TCFU)              ,INTENT(INOUT)            :: YDCFU
TYPE(TRIP)              ,INTENT(IN)               :: YDRIP
TYPE(CPG_OPTS_TYPE)     ,INTENT(IN)               :: YDCPG_OPTS
TYPE(CPG_BNDS_TYPE)     ,INTENT(IN)               :: YDCPG_BNDS
TYPE(MF_PHYS_OUT_TYPE)  ,INTENT(IN)               :: YDMF_PHYS_OUT
TYPE(CPG_MISC_TYPE)     ,INTENT(IN)               :: YDCPG_MISC
TYPE(CPG_DYN_TYPE)      ,INTENT(IN)               :: YDCPG_DYN0
TYPE(FIELD_VARIABLES)   ,INTENT(IN)               :: YDVARS
TYPE(MF_PHYS_SURF_TYPE) ,INTENT(IN)               :: YDMF_PHYS_SURF
REAL(KIND=JPRB)         ,INTENT(IN)               :: PSTRCU(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(IN)               :: PSTRCV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(IN)               :: PSTRDU(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(IN)               :: PSTRDV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(IN)               :: PSTRTU(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(IN)               :: PSTRTV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 

!     ------------------------------------------------------------------

REAL(KIND=JPRB) :: ZFPREC9(YDCPG_OPTS%KLON) ! past cumulated flux of total precipitations
REAL(KIND=JPRB) :: ZFPREC0(YDCPG_OPTS%KLON) ! instantaneous flux of total precipitations
REAL(KIND=JPRB) :: ZSQPRE(YDCPG_OPTS%KLON)  ! instantaneous flux of total squared precipitations

REAL(KIND=JPRB), PARAMETER :: ZEPS = EPSILON (1.0_JPRB) * 100._JPRB

REAL(KIND=JPRB) :: ZMUL
INTEGER(KIND=JPIM) :: JLON, JLEV
INTEGER(KIND=JPIM) :: ITIME
LOGICAL :: LLFRRC, LLRESET, LLNORESET

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CPCFU',0,ZHOOK_HANDLE)

ASSOCIATE(LREACFU=>YDCFU%LREACFU, NFRRC=>YDCFU%NFRRC, TSTEP=>YDRIP%TSTEP)
!     ------------------------------------------------------------------

!*    1.    PREPARATIONS

!*    1.1   READ IN FIELDS

IF (.NOT.YDCPG_OPTS%LFSTEP .OR. LREACFU) THEN
  LLRESET = .FALSE.
ELSE
  LLRESET = .TRUE.
ENDIF

!*    1.2  SAVE TOTAL PRECIPITATIONS CUMULATED FLUX

IF (YDCFU%YFDUTP%LACTIVE) THEN

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

  ZFPREC9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0.0_JPRB
  ZFPREC0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0.0_JPRB
  ZSQPRE(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0.0_JPRB

  CALL ACCUM3_CFU (YDCFU%YFPLCL, YDCFU%FPLCL, YDMF_PHYS_OUT%FPLCL)
  CALL ACCUM3_CFU (YDCFU%YFPLCN, YDCFU%FPLCN, YDMF_PHYS_OUT%FPLCN)
  CALL ACCUM3_CFU (YDCFU%YFPLCG, YDCFU%FPLCG, YDMF_PHYS_OUT%FPLCG)
  CALL ACCUM3_CFU (YDCFU%YFPLCH, YDCFU%FPLCH, YDMF_PHYS_OUT%FPLCH)
  CALL ACCUM3_CFU (YDCFU%YFPLSL, YDCFU%FPLSL, YDMF_PHYS_OUT%FPLSL)
  CALL ACCUM3_CFU (YDCFU%YFPLSN, YDCFU%FPLSN, YDMF_PHYS_OUT%FPLSN)
  CALL ACCUM3_CFU (YDCFU%YFPLSG, YDCFU%FPLSG, YDMF_PHYS_OUT%FPLSG)
  CALL ACCUM3_CFU (YDCFU%YFPLSH, YDCFU%FPLSH, YDMF_PHYS_OUT%FPLSH)

  ZSQPRE(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=(ZFPREC0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)**2)*TSTEP
  ZFPREC0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=ZFPREC0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)*TSTEP

  IF (LLRESET) YDCFU%FDUTP (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = 0._JPRB

  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
    IF (ZFPREC9(JLON) > ZEPS) THEN
      YDCFU%FDUTP(JLON)=YDCFU%FDUTP(JLON)*((ZFPREC9(JLON)+ZFPREC0(JLON))**2)&
       & /((ZFPREC9(JLON)**2)+(YDCFU%FDUTP(JLON)*ZSQPRE(JLON)))  
    ELSEIF (ZFPREC0(JLON) > ZEPS) THEN
      YDCFU%FDUTP(JLON)=TSTEP
    ELSE
      YDCFU%FDUTP(JLON)=0.0_JPRB
    ENDIF
  ENDDO

!$ACDC }

ENDIF

!     ------------------------------------------------------------------

!*    2.    ACCUMULATE FLUXES.
!           ------------------

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

IF (YDCFU%YSTRDU%LACTIVE)                               CALL COPY3_CFU (YDCFU%YSTRDU, YDCFU%STRDU, PSTRDU)
IF (YDCFU%YSTRDV%LACTIVE)                               CALL COPY3_CFU (YDCFU%YSTRDV, YDCFU%STRDV, PSTRDV)
IF (YDCFU%YSTRCU%LACTIVE)                               CALL COPY3_CFU (YDCFU%YSTRCU, YDCFU%STRCU, PSTRCU)
IF (YDCFU%YSTRCV%LACTIVE)                               CALL COPY3_CFU (YDCFU%YSTRCV, YDCFU%STRCV, PSTRCV)
IF (YDCFU%YFDICQ%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFDICQ, YDCFU%FDICQ, YDMF_PHYS_OUT%DIFCQ)
IF (YDCFU%YFDICS%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFDICS, YDCFU%FDICS, YDMF_PHYS_OUT%DIFCS)
IF (YDCFU%YSTRTU%LACTIVE)                               CALL COPY3_CFU (YDCFU%YSTRTU, YDCFU%STRTU, PSTRTU)
IF (YDCFU%YSTRTV%LACTIVE)                               CALL COPY3_CFU (YDCFU%YSTRTV, YDCFU%STRTV, PSTRTV)
IF (YDCFU%YFDITQ%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFDITQ, YDCFU%FDITQ, YDMF_PHYS_OUT%DIFTQ)
IF (YDCFU%YFDITS%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFDITS, YDCFU%FDITS, YDMF_PHYS_OUT%DIFTS)
IF (YDCFU%YFPLCL%LACTIVE .AND. .NOT.YDCPG_OPTS%LCORWAT) CALL COPY3_CFU (YDCFU%YFPLCL, YDCFU%FPLCL, YDMF_PHYS_OUT%FPLCL)
IF (YDCFU%YFCCQL%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFCCQL, YDCFU%FCCQL, YDMF_PHYS_OUT%FCCQL)
IF (YDCFU%YFPLCN%LACTIVE .AND. .NOT.YDCPG_OPTS%LCORWAT) CALL COPY3_CFU (YDCFU%YFPLCN, YDCFU%FPLCN, YDMF_PHYS_OUT%FPLCN)
IF (YDCFU%YFPLCG%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFPLCG, YDCFU%FPLCG, YDMF_PHYS_OUT%FPLCG)
IF (YDCFU%YFPLCH%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFPLCH, YDCFU%FPLCH, YDMF_PHYS_OUT%FPLCH)
IF (YDCFU%YFCCQN%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFCCQN, YDCFU%FCCQN, YDMF_PHYS_OUT%FCCQN)
IF (YDCFU%YFPLSL%LACTIVE .AND. .NOT.YDCPG_OPTS%LCORWAT) CALL COPY3_CFU (YDCFU%YFPLSL, YDCFU%FPLSL, YDMF_PHYS_OUT%FPLSL)
IF (YDCFU%YFCSQL%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFCSQL, YDCFU%FCSQL, YDMF_PHYS_OUT%FCSQL)
IF (YDCFU%YFPLSN%LACTIVE .AND. .NOT.YDCPG_OPTS%LCORWAT) CALL COPY3_CFU (YDCFU%YFPLSN, YDCFU%FPLSN, YDMF_PHYS_OUT%FPLSN)
IF (YDCFU%YFPLSG%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFPLSG, YDCFU%FPLSG, YDMF_PHYS_OUT%FPLSG)
IF (YDCFU%YFPLSH%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFPLSH, YDCFU%FPLSH, YDMF_PHYS_OUT%FPLSH)
IF (YDCFU%YFCSQN%LACTIVE)                               CALL COPY3_CFU (YDCFU%YFCSQN, YDCFU%FCSQN, YDMF_PHYS_OUT%FCSQN)
IF (YDCFU%YFRSO%LACTIVE )                               CALL COPY3_CFU (YDCFU%YFRSO , YDCFU%FRSO , YDMF_PHYS_OUT%FRSO(:,:,1))
IF (YDCFU%YFRTH%LACTIVE )                               CALL COPY3_CFU (YDCFU%YFRTH , YDCFU%FRTH , YDMF_PHYS_OUT%FRTH(:,:,1))
IF (YDCFU%YFNEB%LACTIVE )                               CALL COPY3_CFU (YDCFU%YFNEB , YDCFU%FNEB , YDCPG_MISC%NEB)

!$ACDC }

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

IF (YDCFU%YFNEBT%LACTIVE) CALL COPY1_CFU (YDCFU%YFNEBT, YDCFU%FNEBT, YDCPG_MISC%CLCT)
IF (YDCFU%YFCLL%LACTIVE ) CALL COPY1_CFU (YDCFU%YFCLL , YDCFU%FCLL , YDMF_PHYS_OUT%FCLL(:,1))
IF (YDCFU%YFCLN%LACTIVE ) CALL COPY1_CFU (YDCFU%YFCLN , YDCFU%FCLN , YDMF_PHYS_OUT%FCLN(:,1))
IF (YDCFU%YFEVL%LACTIVE ) CALL COPY1_CFU (YDCFU%YFEVL , YDCFU%FEVL , YDMF_PHYS_OUT%FEVL(:,1))
IF (YDCFU%YFEVN%LACTIVE ) CALL COPY1_CFU (YDCFU%YFEVN , YDCFU%FEVN , YDMF_PHYS_OUT%FEVN(:,1))
IF (YDCFU%YFONTE%LACTIVE) CALL COPY1_CFU (YDCFU%YFONTE, YDCFU%FONTE, YDMF_PHYS_OUT%FONTE)
IF (YDCFU%YFCHSP%LACTIVE) CALL COPY1_CFU (YDCFU%YFCHSP, YDCFU%FCHSP, YDMF_PHYS_OUT%FCHSP(:,1))
IF (YDCFU%YFLWSP%LACTIVE) CALL COPY1_CFU (YDCFU%YFLWSP, YDCFU%FLWSP, YDMF_PHYS_OUT%FLWSP)
IF (YDCFU%YRUISS%LACTIVE) CALL COPY1_CFU (YDCFU%YRUISS, YDCFU%RUISS, YDMF_PHYS_OUT%RUISS)
IF (YDCFU%YRUISP%LACTIVE) CALL COPY1_CFU (YDCFU%YRUISP, YDCFU%RUISP, YDMF_PHYS_OUT%RUISP)
IF (YDCFU%YFEVV%LACTIVE ) CALL COPY1_CFU (YDCFU%YFEVV , YDCFU%FEVV , YDMF_PHYS_OUT%FEVV)
IF (YDCFU%YFTR%LACTIVE  ) CALL COPY1_CFU (YDCFU%YFTR  , YDCFU%FTR  , YDMF_PHYS_OUT%FTR)
IF (YDCFU%YRUISL%LACTIVE) CALL COPY1_CFU (YDCFU%YRUISL, YDCFU%RUISL, YDMF_PHYS_OUT%RUISL)
IF (YDCFU%YFGEL%LACTIVE ) CALL COPY1_CFU (YDCFU%YFGEL , YDCFU%FGEL , YDMF_PHYS_OUT%FGEL)
IF (YDCFU%YFGELS%LACTIVE) CALL COPY1_CFU (YDCFU%YFGELS, YDCFU%FGELS, YDMF_PHYS_OUT%FGELS)
IF (YDCFU%YFCS%LACTIVE  ) CALL COPY1_CFU (YDCFU%YFCS  , YDCFU%FCS  , YDMF_PHYS_OUT%FCS(:,1))
IF (YDCFU%YWS%LACTIVE   ) CALL COPY1_CFU (YDCFU%YWS   , YDCFU%WS   , YDMF_PHYS_SURF%GSP_RR%PW_T0)
IF (YDCFU%YSNS%LACTIVE  ) CALL COPY1_CFU (YDCFU%YSNS  , YDCFU%SNS  , YDMF_PHYS_SURF%GSP_SG%PF_T0(:,1))
IF (YDCFU%YSPRES%LACTIVE) CALL COPY1_CFU (YDCFU%YSPRES, YDCFU%SPRES, YDCPG_DYN0%PRE(:,YDCPG_OPTS%KFLEVG))
IF (YDCFU%YFDISH%LACTIVE) CALL COPY1_CFU (YDCFU%YFDISH, YDCFU%FDISH, YDMF_PHYS_OUT%FDIS(:,YDCPG_OPTS%KFLEVG))
IF (YDCFU%YFTOPH%LACTIVE) CALL COPY1_CFU (YDCFU%YFTOPH, YDCFU%FTOPH, YDMF_PHYS_OUT%FRMH(:,0))

!$ACDC }

IF (NFRRC /= 0) THEN
  LLFRRC = MOD(YDCPG_OPTS%NSTEP,NFRRC) == 0
ELSE
  LLFRRC = .FALSE.
ENDIF

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

IF (YDCFU%YFRSODS%LACTIVE)              CALL COPY1_CFU (YDCFU%YFRSODS, YDCFU%FRSODS, YDMF_PHYS_OUT%FRSODS)
IF (YDCFU%YFRSDNI%LACTIVE)              CALL COPY1_CFU (YDCFU%YFRSDNI, YDCFU%FRSDNI, YDMF_PHYS_OUT%FRSDNI)
IF (YDCFU%YFRSGNI%LACTIVE)              CALL COPY1_CFU (YDCFU%YFRSGNI, YDCFU%FRSGNI, YDMF_PHYS_OUT%FRSGNI)
IF (YDCFU%YFRSOPS%LACTIVE)              CALL COPY1_CFU (YDCFU%YFRSOPS, YDCFU%FRSOPS, YDMF_PHYS_OUT%FRSOPS)
IF (YDCFU%YFRSOPT%LACTIVE)              CALL COPY1_CFU (YDCFU%YFRSOPT, YDCFU%FRSOPT, YDMF_PHYS_OUT%FRSOPT)
IF (YDCFU%YFRTHDS%LACTIVE)              CALL COPY1_CFU (YDCFU%YFRTHDS, YDCFU%FRTHDS, YDMF_PHYS_OUT%FRTHDS)
IF (YDCFU%YFRSOLU%LACTIVE)              CALL COPY1_CFU (YDCFU%YFRSOLU, YDCFU%FRSOLU, YDMF_PHYS_OUT%FRSOLU)
IF (YDCFU%YNEBCON%LACTIVE)              CALL COPY1_CFU (YDCFU%YNEBCON, YDCFU%NEBCON, YDMF_PHYS_OUT%CLCC  )
IF (YDCFU%YNEBHAU%LACTIVE)              CALL COPY1_CFU (YDCFU%YNEBHAU, YDCFU%NEBHAU, YDMF_PHYS_OUT%CLCH  )
IF (YDCFU%YNEBMOY%LACTIVE)              CALL COPY1_CFU (YDCFU%YNEBMOY, YDCFU%NEBMOY, YDMF_PHYS_OUT%CLCM  )
IF (YDCFU%YNEBBAS%LACTIVE)              CALL COPY1_CFU (YDCFU%YNEBBAS, YDCFU%NEBBAS, YDMF_PHYS_OUT%CLCL  )
IF (YDCFU%YFRSOC0%LACTIVE .AND. LLFRRC) CALL COPY1_CFU (YDCFU%YFRSOC0, YDCFU%FRSOC0, YDMF_PHYS_OUT%FRSOC(:,0), NFRRC)
IF (YDCFU%YFRSOC1%LACTIVE .AND. LLFRRC) CALL COPY1_CFU (YDCFU%YFRSOC1, YDCFU%FRSOC1, YDMF_PHYS_OUT%FRSOC(:,1), NFRRC)
IF (YDCFU%YFRTHC0%LACTIVE .AND. LLFRRC) CALL COPY1_CFU (YDCFU%YFRTHC0, YDCFU%FRTHC0, YDMF_PHYS_OUT%FRTHC(:,0), NFRRC)
IF (YDCFU%YFRTHC1%LACTIVE .AND. LLFRRC) CALL COPY1_CFU (YDCFU%YFRTHC1, YDCFU%FRTHC1, YDMF_PHYS_OUT%FRTHC(:,1), NFRRC)
IF (YDCFU%YFLASH%LACTIVE)               CALL COPY1_CFU (YDCFU%YFLASH , YDCFU%FLASH , YDMF_PHYS_OUT%FLASH )
IF (YDCFU%YLOADSNOW%LACTIVE)            CALL COPY1_CFU (YDCFU%YLOADSNOW, YDCFU%LOADSNOW, YDMF_PHYS_OUT%LOADSNOW)
IF (YDCFU%YLOADICE%LACTIVE)             CALL COPY1_CFU (YDCFU%YLOADICE, YDCFU%LOADICE, YDMF_PHYS_OUT%LOADICE)
IF (YDCFU%YDIAMACCR%LACTIVE)            CALL COPY1_CFU (YDCFU%YDIAMACCR, YDCFU%DIAMACCR, YDMF_PHYS_OUT%DIAMACCR)
IF (YDCFU%YPCPWSNOW%LACTIVE)            CALL COPY1_CFU (YDCFU%YPCPWSNOW, YDCFU%PCPWSNOW, YDMF_PHYS_OUT%PCPWSNOW)
IF (YDCFU%YPCPICE%LACTIVE)              CALL COPY1_CFU (YDCFU%YPCPICE, YDCFU%PCPICE, YDMF_PHYS_OUT%PCPICE)

!$ACDC }

IF (YDCFU%YFCL%LACTIVE) THEN

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

  CALL COPY1_CFU (YDCFU%YFCL, YDCFU%FCL, YDMF_PHYS_OUT%FCLL(:,1))
  CALL COPY1_CFU (YDCFU%YFCL, YDCFU%FCL, YDMF_PHYS_OUT%FCLN(:,1), LDNORESET=.TRUE.)

!$ACDC }

ENDIF

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

IF (YDCFU%YQTOT%LACTIVE ) CALL COPY31M_CFU (YDCFU%YQTOT , YDCFU%QTOT , YDVARS%Q%T0    )
IF (YDCFU%YQICE%LACTIVE ) CALL COPY31M_CFU (YDCFU%YQICE , YDCFU%QICE , YDCPG_MISC%QICE)
IF (YDCFU%YQLI%LACTIVE  ) CALL COPY31M_CFU (YDCFU%YQLI  , YDCFU%QLI  , YDCPG_MISC%QLI )
IF (YDCFU%YOZONT%LACTIVE) CALL COPY31M_CFU (YDCFU%YOZONT, YDCFU%OZONT, YDVARS%O3%T0   )

!$ACDC }

!     ------------------------------------------------------------------

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('CPCFU',1,ZHOOK_HANDLE)

CONTAINS

SUBROUTINE ACCUM3_CFU (YDDESC, PXFU, PPHY)

TYPE(FLUXES_DESCRIPTOR) ,INTENT(IN)               :: YDDESC
REAL(KIND=JPRB)         ,INTENT(IN)               :: PXFU(:,:)
REAL(KIND=JPRB)         ,INTENT(IN)               :: PPHY(1:YDCPG_OPTS%KLON,YDDESC%ILEVBOT:YDCPG_OPTS%KFLEVG)

IF (YDDESC%LACTIVE) THEN
  JLEV = YDDESC%ILEV
  
  IF (.NOT. LLRESET) THEN
    ZFPREC9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=ZFPREC9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)+&
                   & PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
  ENDIF
  
  ZFPREC0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=ZFPREC0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)+&
                 & PPHY(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,YDDESC%INUMLEV(JLEV))
ENDIF

END SUBROUTINE 

SUBROUTINE COPY1_CFU (YDDESC, PXFU, PPHY, KTIME, LDNORESET)

TYPE(FLUXES_DESCRIPTOR) ,INTENT(IN)               :: YDDESC
REAL(KIND=JPRB)         ,INTENT(INOUT)            :: PXFU(:)
REAL(KIND=JPRB)         ,INTENT(IN)               :: PPHY(1:YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM)      ,INTENT(IN)    ,OPTIONAL  :: KTIME
LOGICAL                 ,INTENT(IN)    ,OPTIONAL  :: LDNORESET

LLNORESET = .FALSE.
IF (PRESENT (LDNORESET)) LLNORESET = LDNORESET

IF (LLRESET .AND. (.NOT. LLNORESET)) THEN
  PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0._JPRB
ENDIF

ITIME = 1
IF (PRESENT (KTIME)) ITIME = KTIME

PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)+&
               & PPHY(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)*YDRIP%TSTEP*REAL(ITIME,JPRB)

END SUBROUTINE 

SUBROUTINE COPY3_CFU (YDDESC, PXFU, PPHY)

TYPE(FLUXES_DESCRIPTOR) ,INTENT(IN)               :: YDDESC
REAL(KIND=JPRB)         ,INTENT(INOUT)            :: PXFU(:,:)
REAL(KIND=JPRB)         ,INTENT(IN)               :: PPHY(1:YDCPG_OPTS%KLON,YDDESC%ILEVBOT:YDCPG_OPTS%KFLEVG)

IF (LLRESET) THEN
  PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,:)=0._JPRB
ENDIF

DO JLEV = 1, YDDESC%ILEV
  PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)=PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)+&
               & PPHY(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,YDDESC%INUMLEV(JLEV))*YDRIP%TSTEP
ENDDO

END SUBROUTINE 

SUBROUTINE COPY31M_CFU (YDDESC, PXFU, PPHY)

TYPE(FLUXES_DESCRIPTOR) ,INTENT(IN)               :: YDDESC
REAL(KIND=JPRB)         ,INTENT(INOUT)            :: PXFU(:)
REAL(KIND=JPRB)         ,INTENT(IN)               :: PPHY(1:YDCPG_OPTS%KLON,1:YDCPG_OPTS%KFLEVG)

IF (LLRESET) THEN
  PXFU(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0._JPRB
ENDIF

ZMUL = 1._JPRB/YDCST%RG

DO JLEV = 1, YDCPG_OPTS%KFLEVG
  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
    PXFU(JLON)=PXFU(JLON)+PPHY(JLON,JLEV)*YDCPG_DYN0%XYB%DELP(JLON,JLEV)*ZMUL*YDRIP%TSTEP
  ENDDO
ENDDO

END SUBROUTINE 

END SUBROUTINE CPCFU


