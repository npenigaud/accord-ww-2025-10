SUBROUTINE MCOORDS(YDSCO,YDRIP,KFLEV,KPROMA,KST,KEND,PA,PGEMU,PGSQM2,PGNORDM,PGNORDL,&
  &PU,PV,PSCO,LD2TL,LD2TLM,LDROT)

!$ACDC singlecolumn  --process-pointers

! Purpose:
!  compute coordinates (in PSCO) of the 'M' point (middle point of SL trajectory)
!
! Input:
!  PA: RA
!  PGEMU/GSQM2/GECLO/GESLO: geographical coordinates of grid-points
!  PU/PV: U/V wind components ('pseudo-trajectory' wind)
!  LD2TL: switch for 2TL/3TL SL scheme (= LTWOTL)
!  LD2TLM: switch indicating how the position of M is computed
!  LDROT: switch for 'North-rotating' or not the input 'pseudo-trajectory' wind (PU/PV)
!
! Output:
!  PSCO: array of spherical coordinates of the 'M' point
!
! Author:
!  H Petithomme, Dec 2023: extracted from larmes/lapineb

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK
USE FIELD_VARIABLES_MOD, ONLY: GEOMETRY_VARIABLES
USE INTDYNSL_MOD, ONLY : TSCO
USE YOMRIP, ONLY: TRIP

IMPLICIT NONE

TYPE(TSCO)         ,INTENT(IN)             :: YDSCO
TYPE(TRIP)         ,INTENT(IN)             :: YDRIP
INTEGER(KIND=JPIM) ,INTENT(IN)             :: KFLEV,KPROMA,KST,KEND
LOGICAL            ,INTENT(IN)             :: LD2TL,LD2TLM
LOGICAL            ,INTENT(IN)  ,OPTIONAL  :: LDROT
REAL(KIND=JPRB)    ,INTENT(IN)             :: PA
REAL(KIND=JPRB)    ,INTENT(IN)             :: PGEMU(KPROMA),PGSQM2(KPROMA),PGNORDM(KPROMA),PGNORDL(KPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)             :: PU(KPROMA,KFLEV)
REAL(KIND=JPRB)    ,INTENT(IN)             :: PV(KPROMA,KFLEV)
REAL(KIND=JPRB)    ,INTENT(OUT)            :: PSCO(KPROMA,KFLEV,YDSCO%NDIM)

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLROT
REAL(KIND=JPRB) :: ZSINPHI,ZINT,ZCOPHI,ZNOR2,ZU,ZV,ZPHI2,ZDTSA,ZDTS22,ZDTS62
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

#include "rotate.func.h"

IF (LHOOK) CALL DR_HOOK("MCOORDS",0,ZHOOK_HANDLE)

ASSOCIATE(RTDT=>YDRIP%RTDT,RDTSA=>YDRIP%RDTSA,RDTS22=>YDRIP%RDTS22,RDTS62=>YDRIP%RDTS62)

LLROT = .FALSE.
IF (PRESENT(LDROT)) LLROT = LDROT

! RTDT: TDT
! RDTSA: TDT/RA
! RDTSA2: RDTSA**2
! RDTS62: RDTSA**2/6
! RDTS22: RDTSA**2/2

IF (LD2TL) THEN
  ZDTSA = 2*RDTSA
  ZDTS22 = 4*RDTS22
  ZDTS62 = 4*RDTS62
ELSE
  ZDTSA = RDTSA
  ZDTS22 = RDTS22
  ZDTS62 = RDTS62
ENDIF

! LD2TLM is LLO in larmes (=LTWOTL.AND..NOT.LELTRA), else is SL3TL or L2TLFF in lapineb
IF (LD2TL.AND..NOT.LD2TLM) THEN
  DO JLEV=1,KFLEV
    DO JROF=KST,KEND
      ZNOR2 = PU(JROF,JLEV)**2+PV(JROF,JLEV)**2
      ZPHI2 = (RTDT*SQRT(ZNOR2)/PA/2)**2
      !ZCOPHI = 1-RDTS22*ZNOR2
      !ZSINPHI = ZCOPHI*RDTSA*(1-RDTS62*ZNOR2)
      !ZPHI = RTDT*SQRT(ZNOR2)/PA
      ZCOPHI = 1-ZPHI2/2
      ZSINPHI = ZCOPHI*RTDT*(1-ZPHI2/6)/PA
      !ZSINPHI = ZCOPHI*ZSINPHI
      ZCOPHI = 2*ZCOPHI**2-1
      PSCO(JROF,JLEV,YDSCO%M_SINLA) = PGEMU(JROF)*ZCOPHI-PGSQM2(JROF)*PV(JROF,JLEV)*ZSINPHI
      PSCO(JROF,JLEV,YDSCO%M_COSCO) = PGSQM2(JROF)*ZCOPHI+PGEMU(JROF)*PV(JROF,JLEV)*ZSINPHI
      PSCO(JROF,JLEV,YDSCO%M_SINCO) = -PU(JROF,JLEV)*ZSINPHI
      PSCO(JROF,JLEV,YDSCO%M_COPHI) = ZCOPHI
    ENDDO
  ENDDO
ELSE
  ! phi=|v|*tdt/a, cos(phi)=1-phi**2/2+o(phi**2), sin(phi)=phi*(1-phi**2/6)+o(phi**3)
  ! phi=zdtsa*sqrt(znor2), zcophi=cos(phi), zsinphi=sin(phi)
  DO JLEV=1,KFLEV
    DO JROF=KST,KEND
      ZNOR2 = PU(JROF,JLEV)**2+PV(JROF,JLEV)**2
      ZCOPHI = 1-ZDTS22*ZNOR2
      ZSINPHI = ZDTSA*(1-ZDTS62*ZNOR2)

      IF (LD2TL.AND..NOT.LD2TLM) THEN
        ZPHI2 = (RTDT*SQRT(ZNOR2)/PA/2)**2
        !ZCOPHI = 1-RDTS22*ZNOR2
        !ZSINPHI = ZCOPHI*RDTSA*(1-RDTS62*ZNOR2)
        !ZPHI = RTDT*SQRT(ZNOR2)/PA
        ZCOPHI = 1-ZPHI2/2
        ZSINPHI = ZCOPHI*RTDT*(1-ZPHI2/6)/PA
        !ZSINPHI = ZCOPHI*ZSINPHI
        ZCOPHI = 2*ZCOPHI**2-1
      ENDIF

      IF (LLROT) THEN
        ZU = FROTU(PU(JROF,JLEV),PV(JROF,JLEV),PGNORDM(JROF),PGNORDL(JROF))
        ZV = FROTV(PU(JROF,JLEV),PV(JROF,JLEV),PGNORDM(JROF),PGNORDL(JROF))
      ELSE
        ZU = PU(JROF,JLEV)
        ZV = PV(JROF,JLEV)
      ENDIF

      ! geo projected coordinates of point M
      ZINT = ZV*ZSINPHI
      PSCO(JROF,JLEV,YDSCO%M_SINLA) = PGEMU(JROF)*ZCOPHI-PGSQM2(JROF)*ZINT
      PSCO(JROF,JLEV,YDSCO%M_COSCO) = PGSQM2(JROF)*ZCOPHI+PGEMU(JROF)*ZINT
      PSCO(JROF,JLEV,YDSCO%M_SINCO) = -ZU*ZSINPHI
      PSCO(JROF,JLEV,YDSCO%M_COPHI) = ZCOPHI
    ENDDO
  ENDDO
ENDIF
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK("MCOORDS",1,ZHOOK_HANDLE)
END SUBROUTINE MCOORDS

