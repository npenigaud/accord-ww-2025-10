SUBROUTINE APL_ARPEGE_MWAVE (YDCPG_MISC, YDMF_PHYS, YDCPG_BNDS, YDCPG_OPTS, YDPHYSMWAVE) 

!$ACDC outline1    


USE PARKIND1         , ONLY : JPIM, JPRB
USE CPG_TYPE_MOD     , ONLY : CPG_MISC_TYPE
USE MF_PHYS_TYPE_MOD , ONLY : MF_PHYS_TYPE
USE YOMHOOK          , ONLY : DR_HOOK, JPHOOK, LHOOK
USE YOE_PHYS_MWAVE   , ONLY : TEPHYSMWAVE
USE CPG_OPTS_TYPE_MOD, ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE


IMPLICIT NONE

TYPE(CPG_MISC_TYPE) ,INTENT(IN)     :: YDCPG_MISC
TYPE(MF_PHYS_TYPE)  ,INTENT(IN)     :: YDMF_PHYS
TYPE(CPG_BNDS_TYPE) ,INTENT(IN)     :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE) ,INTENT(IN)     :: YDCPG_OPTS
TYPE(TEPHYSMWAVE)   ,INTENT(INOUT)  :: YDPHYSMWAVE 

REAL(KIND=JPRB) :: ZCOVPTOT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
INTEGER(KIND=JPIM) :: JLON, JLEV

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('APL_ARPEGE_MWAVE',0,ZHOOK_HANDLE)

ASSOCIATE (KLON => YDCPG_OPTS%KLON, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA, KFLEVG => YDCPG_OPTS%KFLEVG)

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

ZCOVPTOT(:, :) = 0.0_JPRB

DO JLEV= 1, KFLEVG
  DO JLON = KIDIA, KFDIA
    IF (JLEV>0) THEN
       IF (ABS(0.5 * (YDMF_PHYS%OUT%FPLSL(JLON,JLEV) + YDMF_PHYS%OUT%FPLSL(JLON,JLEV-1)) + &
            &  0.5 * (YDMF_PHYS%OUT%FPLSN(JLON,JLEV) + YDMF_PHYS%OUT%FPLSN(JLON,JLEV-1)) + &
            &  0.5 * (YDMF_PHYS%OUT%FPLCN(JLON,JLEV) + YDMF_PHYS%OUT%FPLCN(JLON,JLEV-1)) + &
            &  0.5 * (YDMF_PHYS%OUT%FPLCL(JLON,JLEV) + YDMF_PHYS%OUT%FPLCL(JLON,JLEV-1)) ) >=1.E-25_JPRB) THEN
          IF ( JLEV == 1_JPIM) THEN
            ZCOVPTOT(JLON, JLEV) = 1.0_JPRB - ((1.0_JPRB-ZCOVPTOT(JLON,JLEV))*&
                                 & (1.0_JPRB -YDCPG_MISC%NEB(JLON,JLEV))/ &
                                 &  1.E-06_JPRB )
          ELSE
            ZCOVPTOT(JLON, JLEV) = 1.0_JPRB - ((1.0_JPRB-ZCOVPTOT(JLON,JLEV-1))*&
                                 & (1.0_JPRB -MAX(YDCPG_MISC%NEB(JLON,JLEV),YDCPG_MISC%NEB(JLON,JLEV-1)))/ &
                                 & (1.0_JPRB -MIN(YDCPG_MISC%NEB(JLON,JLEV-1),1.0_JPRB-1.E-06_JPRB)) )
          ENDIF
          ZCOVPTOT(JLON, JLEV) = MAX(ZCOVPTOT(JLON, JLEV), 0.1_JPRB)
       ENDIF
    ENDIF ! MARYTEMP sur JLEV
    YDPHYSMWAVE%P(JLON,JLEV,1) = YDCPG_MISC%QLI(JLON,JLEV)  !PQLI(JLON,JLEV) ! liquid cloud water
    YDPHYSMWAVE%P(JLON,JLEV,2) = YDCPG_MISC%QICE(JLON,JLEV)  !PQICE(JLON,JLEV) ! ice cloud water  
    YDPHYSMWAVE%P(JLON,JLEV,3) = YDCPG_MISC%NEB(JLON,JLEV) ! cloud fraction    
    YDPHYSMWAVE%P(JLON,JLEV,4) = 0.5 * (YDMF_PHYS%OUT%FPLSL(JLON,JLEV) + YDMF_PHYS%OUT%FPLSL(JLON,JLEV-1)) ! STRATIFORM PRECIP as rain
    YDPHYSMWAVE%P(JLON,JLEV,5) = 0.5 * (YDMF_PHYS%OUT%FPLSN(JLON,JLEV) + YDMF_PHYS%OUT%FPLSN(JLON,JLEV-1)) !  STRATIFORM PRECIP as snow
    YDPHYSMWAVE%P(JLON,JLEV,6) = 0.5 * (YDMF_PHYS%OUT%FPLCL(JLON,JLEV) + YDMF_PHYS%OUT%FPLCL(JLON,JLEV-1)) ! CONVECTIVE PRECIPITATION AS RAIN
    YDPHYSMWAVE%P(JLON,JLEV,7) = 0.5 * (YDMF_PHYS%OUT%FPLCN(JLON,JLEV) + YDMF_PHYS%OUT%FPLCN(JLON,JLEV-1)) ! CONVECTIVE PRECIPITATION AS SNOW 
    YDPHYSMWAVE%P(JLON,JLEV,8) = ZCOVPTOT(JLON, JLEV) ! PCOVTOT:PRECIPITATION FRACTION IN EACH LAYER
  ENDDO
ENDDO

DO JLON = KIDIA, KFDIA
  YDPHYSMWAVE%P(JLON,1,9) = 0._JPRB ! PDIAG%IPBLTYPE(JL) in CALLPAR
  YDPHYSMWAVE%P(JLON,2,9) = 0._JPRB ! PDIAG%IPBLTYPE(JL) in CALLPAR 
  YDPHYSMWAVE%P(JLON,3:KFLEVG,9) = 0._JPRB
ENDDO

!$ACDC }

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('APL_ARPEGE_MWAVE',1,ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_MWAVE

