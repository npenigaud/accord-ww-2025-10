SUBROUTINE CPGLAG (YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDCPG_SL2, YDCPG_DDH_TNDHD, YDMODEL, &
& YDVARS,  LDCONFX, YDDDH, YDA_PGPSTRESS, YDTDDH, PTRAJEC, PTRAJEC_OOPS)

!$ACDC outline1    


!**** *CPGLAG* - Grid point calculations - lagged part.

!     Purpose.
!     --------
!           Grid point calculations lagged part.
!
!**   Interface.
!     ----------
!        *CALL* *CPGLAG*

!        Explicit arguments :
!        --------------------

!        KGPCOMP       : number of elements of arrays for which computations
!                        are performed (used in message passing version)
!        KSTGLO        : global offset.
!        LDCONFX       : (see in CPG)
!        YDDDH         : DDH structure
!        PGPSTRESS     : (optional) non-linear stress contribution

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!        Called by GP_MODEL.

!     Reference.
!     ----------
!        ARPEGE documentation vol 2 ch 1 and vol 3 ch 6

!     Author.
!     -------
!      Mats Hamrud  *ECMWF*
!      Original : 92-02-01

! Modifications
! -------------
!   N. Wedi and K. Yessad (Nov 2007): ND4SYS to improve NH model stability.
!   K. Yessad (Dec 2008): remove dummy CDLOCK + cleanings
!   K. Yessad (Dec 2009): prune lpc_old.
!   F. Voitus (Feb 2012): introduce ddh diagnostic for semi-lagrangian advection
!   K. Yessad (Oct 2011): in-line GPENDTR
!   F. Vana   19-03-2012: storing t+dt trajectories of T and q
!   N. Wedi   (Nov 2011): add LGPSTRESS option
!   K. Yessad (Nov 2012): simplify testings.
!   T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!   F. Vana  28-Nov-2013 : Redesigned trajectory handling
!   K. Yessad (July 2014): Move some variables.
!   K. Yessad (June 2017): Introduce NHQE model.
!   J. Vivoda and P. Smolikova (Aug-2023): Blended NH/HYD system.
!   F. Voitus (Sep-2023): Blende NH/QE system and new ND4SYS option
!     ------------------------------------------------------------------

USE MODEL_DIAGNOSTICS_MOD  , ONLY : MODEL_DIAGNOSTICS_TYPE
USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE MODEL_DYNAMICS_MOD     , ONLY : MODEL_DYNAMICS_TYPE
USE GEOMETRY_MOD           , ONLY : GEOMETRY
USE YOMGMV                 , ONLY : TGMV
USE PARKIND1               , ONLY : JPIM, JPRB
USE YOMHOOK                , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMTRAJ                , ONLY : TRAJ_TYPE, LPRTTRAJ, LTRAJSAVE, LTRAJSLAG
USE YOMTRAJ_OOPS           , ONLY : TRAJ_TYPE_OOPS, LTRAJSAVE_OOPS, LTRAJSLAG_OOPS
USE YOMLUN                 , ONLY : NULOUT
USE DDH_MIX                , ONLY : TYP_DDH
USE FIELD_VARIABLES_MOD    , ONLY : FIELD_VARIABLES
USE TYPE_MODEL             , ONLY : MODEL
USE CPG_OPTS_TYPE_MOD      , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE CPG_SL2_TYPE_MOD       , ONLY : CPG_SL2_TYPE
USE CPG_DDH_TND_TYPE_MOD   , ONLY : CPG_TNDHD_DDH_TYPE
USE FIELD_ARRAY_MODULE     , ONLY : FIELD_4RB_ARRAY
USE YOMTDDH                , ONLY : TTDDH

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)                ,INTENT(IN)               :: YDGEOMETRY
TYPE(MODEL)                   ,INTENT(IN)               :: YDMODEL
TYPE(FIELD_VARIABLES)         ,INTENT(INOUT)            :: YDVARS
TYPE(CPG_BNDS_TYPE)           ,INTENT(IN)               :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE)           ,INTENT(IN)               :: YDCPG_OPTS
TYPE(CPG_SL2_TYPE)            ,INTENT(IN)               :: YDCPG_SL2
TYPE(CPG_TNDHD_DDH_TYPE)      ,INTENT(INOUT)            :: YDCPG_DDH_TNDHD
LOGICAL                       ,INTENT(IN)               :: LDCONFX
TYPE(TYP_DDH)                 ,INTENT(INOUT)            :: YDDDH
TYPE(FIELD_4RB_ARRAY)         ,INTENT(IN)               :: YDA_PGPSTRESS
TYPE(TTDDH)                   ,INTENT(INOUT)            :: YDTDDH
TYPE(TRAJ_TYPE)               ,INTENT(INOUT) ,OPTIONAL  :: PTRAJEC
TYPE(TRAJ_TYPE_OOPS)          ,INTENT(INOUT) ,OPTIONAL  :: PTRAJEC_OOPS

!     ------------------------------------------------------------------
INTEGER(KIND=JPIM) :: JLEV, JROF

LOGICAL :: LLLSTEP, LLSTR, LLTF1, LL_DIAG_NHX
REAL(KIND=JPRB) :: ZEPS

REAL(KIND=JPRB) :: ZOROGL_SR(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZOROGM_SR(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZGMVNHXT1B(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZR1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGMVSVDT1A(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGMVSVDT1B(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRD_R

!     ------------------------------------------------------------------

#include "gnhx.intfb.h"
#include "gnhqe_svd.intfb.h"
#include "gprcp_expl.intfb.h"
#include "gptf1_ydvars.intfb.h"
#include "cpdyddhlag.intfb.h"

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('CPGLAG',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM, YDDIMV=>YDGEOMETRY%YRDIMV, YDGEM=>YDGEOMETRY%YRGEM, &
& YDLDDH=>YDMODEL%YRML_DIAG%YRLDDH, YGFL=>YDMODEL%YRML_GCONF%YGFL, &
& YDDIMF=>YDMODEL%YRML_GCONF%YRDIMF, YDDYN=>YDMODEL%YRML_DYN%YRDYN, &
& YDDYNA=>YDMODEL%YRML_DYN%YRDYNA)

ASSOCIATE(NPROMA=>YDDIM%NPROMA, NFLEVG=>YDDIMV%NFLEVG, LGPSTRESS=>YDDYN%LGPSTRESS, &
& NCURRENT_ITER=>YDDYN%NCURRENT_ITER, NSITER=>YDDYN%NSITER, RSTRET=>YDGEM%RSTRET,  &
& LRSLDDH=>YDLDDH%LRSLDDH, EPS_NL=>YDDYN%RNHHY_EPS_NL)

!     ------------------------------------------------------------------

!*       1.    INITIAL COMPUTATIONS
!              --------------------

!* This key ensures that at each ICI iteration a guess for NHX(t+dt)
!* is diagnosed from the prognostic variables explicit guesses.
LL_DIAG_NHX = (YDDYNA%ND4SYS==0 .OR. YDDYNA%ND4SYS==3)

!     ------------------------------------------------------------------

!*    2.1   ADD d4 EQN NON ADVECTIVE TENDENCIES OF 'NHX'
!           --------------------------------------------

IF (YDDYNA%LNHDYN .AND. YDDYNA%LSLAG .AND. (YDDYNA%NVDVAR==4 .OR. YDDYNA%NVDVAR==5) &
 & .AND. (YDDYNA%ND4SYS==2 .OR. LL_DIAG_NHX) .AND. (NCURRENT_ITER==NSITER .OR. LL_DIAG_NHX)) THEN

!$ACDC ABORT {

  ZOROGL_SR(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=YDVARS%GEOMETRY%GM%P(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)* &
  &  YDVARS%GEOMETRY%OROGL%P(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
  ZOROGM_SR(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=YDVARS%GEOMETRY%GM%P(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)* &
  &  YDVARS%GEOMETRY%OROGM%P(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)

  ! * Compute a diagnostic value of NHX(t+dt) via GNHX 
  IF (YDDYNA%LNHEE .OR. YDDYNA%LNHHY) THEN

    IF (YDDYNA%LNHHY) THEN
      CALL GNHX(YDGEOMETRY, YDDYNA, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%GEOMETRY%OROG%P,&
       & ZOROGL_SR, ZOROGM_SR, YDVARS%SP%T1, YDVARS%SP%DL, YDVARS%SP%DM, YDVARS%T%T1, YDVARS%T%DL,     &
       & YDVARS%T%DM, YDVARS%Q%T1, YDVARS%Q%DL, YDVARS%Q%DM, YDVARS%L%T1, YDVARS%L%DL, YDVARS%L%DM,    &
       & YDVARS%I%T1, YDVARS%I%DL, YDVARS%I%DM, YDVARS%R%T1, YDVARS%S%T1, YDVARS%G%T1, YDVARS%U%T1,    &
       & YDVARS%V%T1, ZGMVNHXT1B, PEPS_NL=EPS_NL, &
       & PSPD=YDVARS%SPD%T1, PSPDL=YDVARS%SPD%DL, PSPDM=YDVARS%SPD%DM)
    ELSE
      CALL GNHX(YDGEOMETRY, YDDYNA, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%GEOMETRY%OROG%P,&
       & ZOROGL_SR, ZOROGM_SR, YDVARS%SP%T1, YDVARS%SP%DL, YDVARS%SP%DM,YDVARS%T%T1, YDVARS%T%DL,      &
       & YDVARS%T%DM, YDVARS%Q%T1, YDVARS%Q%DL, YDVARS%Q%DM, YDVARS%L%T1,YDVARS%L%DL, YDVARS%L%DM,     &
       & YDVARS%I%T1, YDVARS%I%DL, YDVARS%I%DM, YDVARS%R%T1, YDVARS%S%T1,YDVARS%G%T1, YDVARS%U%T1,     &
       & YDVARS%V%T1, ZGMVNHXT1B, PSPD=YDVARS%SPD%T1,PSPDL=YDVARS%SPD%DL, PSPDM=YDVARS%SPD%DM)
    ENDIF

    IF (LL_DIAG_NHX) THEN
      YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) =  &
       & YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)+&
       & ZGMVNHXT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    ELSE
      IF (YDDYNA%LTWOTL) THEN
        IF( NSITER == 0 ) THEN
          YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) =   &
           & YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) +&
           & ZGMVNHXT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)     &
           & - YDVARS%NHX%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
        ELSE
          YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) =   &
           & YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) +&
           & ZGMVNHXT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)     &
           & - YDVARS%NHX%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
        ENDIF
      ELSE
        YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) =   &
         & YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) +&
         & ZGMVNHXT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)     &
         & - YDVARS%NHX%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
      ENDIF
    ENDIF

  ELSEIF (YDDYNA%LNHQE) THEN

    CALL GNHX(YDGEOMETRY, YDDYNA, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%GEOMETRY%OROG%P,&
     & ZOROGL_SR, ZOROGM_SR, YDVARS%SP%T1, YDVARS%SP%DL, YDVARS%SP%DM, YDVARS%T%T1, YDVARS%T%DL,     &
     & YDVARS%T%DM, YDVARS%Q%T1, YDVARS%Q%DL, YDVARS%Q%DM, YDVARS%L%T1, YDVARS%L%DL, YDVARS%L%DM,    &
     & YDVARS%I%T1, YDVARS%I%DL, YDVARS%I%DM, YDVARS%R%T1, YDVARS%S%T1, YDVARS%G%T1, YDVARS%U%T1,    &
     & YDVARS%V%T1, ZGMVNHXT1B, PSPD=YDVARS%SPD%T1, PSPDL=YDVARS%SPD%DL, PSPDM=YDVARS%SPD%DM)

    IF (.NOT.YDDYNA%LNHQE_SOLVER_QE) THEN
      YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) =  &
       & YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)+&
       & ZGMVNHXT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    ELSE
      
      ZGMVSVDT1A(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) =     &
       & YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)+&
       & ZGMVNHXT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

      CALL GNHQE_SVD(YDGEOMETRY, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, &                        
       & YDVARS%SP%T1, YDVARS%Q%T1, YDVARS%L%T1, YDVARS%I%T1, YDVARS%R%T1,   &
       & YDVARS%S%T1, YDVARS%G%T1, ZGMVSVDT1A, ZGMVSVDT1B)
    
      YDVARS%SVD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) = &
       & ZGMVSVDT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    ENDIF
     
  ENDIF

  ! * Add non advective tendencies of NHX to the RHS of NHX and d4 or d5 eqns.
  YDVARS%NHX%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) = &
   & ZGMVNHXT1B(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

!$ACDC }

ENDIF

!*    2.2   UPDATE U AND V FOR LGPSTRESS OPTION
!           -----------------------------------

! ONLY USED WITH SGS MODEL
IF ((YDDYNA%LPC_FULL .AND. NCURRENT_ITER==NSITER).OR. .NOT.YDDYNA%LPC_FULL) THEN
  IF( LGPSTRESS ) THEN

!$ACDC ABORT {

    YDVARS%U%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=  &
     & YDVARS%U%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)&
     & + YDA_PGPSTRESS%P(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG,1)
    YDVARS%V%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=  &
     & YDVARS%V%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)&
     & + YDA_PGPSTRESS%P(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG,2)


!$ACDC }

  ENDIF
ENDIF

!*    2.3   TIME FILTER (PART 1).
!           ---------------------

IF(YDCPG_OPTS%NSTEP < YDMODEL%YRML_GCONF%YRRIP%NSTOP) THEN
  LLLSTEP=.FALSE.
ELSE
  LLLSTEP=.TRUE.
ENDIF
LLTF1=.FALSE.
IF (NCURRENT_ITER == 0) THEN
  LLTF1=.TRUE.
ENDIF

CALL GPTF1_YDVARS (YDGEOMETRY, YDMODEL%YRML_GCONF, YDDYN, YDDYNA, &
                 & YDVARS, LLTF1, YDCPG_BNDS, YDCPG_OPTS, LLLSTEP)

!*    2.4   Store trajectory of T and Q at t+dt 
!           -----------------------------------

IF(PRESENT(PTRAJEC))THEN
  IF (YDDYNA%LSPRT .AND. LTRAJSAVE .AND. LTRAJSLAG) THEN

!$ACDC ABORT {

    PTRAJEC%SLAG(YDCPG_BNDS%KBL)%PTT15(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%T%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    PTRAJEC%SLAG(YDCPG_BNDS%KBL)%PQT15(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%Q%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    IF (LPRTTRAJ.AND.PTRAJEC%SLAG(YDCPG_BNDS%KBL)%LASTCHUNK)  WRITE(NULOUT,*)'GREPTRAJ ADD TRAJEC%SLAG in CPGLAG'

!$ACDC }

  ENDIF
ELSE
  IF (YDDYNA%LSPRT .AND. LTRAJSAVE_OOPS .AND. LTRAJSLAG_OOPS) THEN

!$ACDC ABORT {

    PTRAJEC_OOPS%SLAG(YDCPG_BNDS%KBL)%PTT15(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%T%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    PTRAJEC_OOPS%SLAG(YDCPG_BNDS%KBL)%PQT15(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%Q%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    IF (PTRAJEC_OOPS%SLAG(YDCPG_BNDS%KBL)%LASTCHUNK)  WRITE(NULOUT,*)'GREPTRAJ ADD TRAJEC%SLAG in CPGLAG'

!$ACDC }

  ENDIF
ENDIF

!*    2.5  TRANSFER FROM BUFFER TO T+DT ARRAYS
!          -----------------------------------

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

DO JLEV = 1, NFLEVG
  DO JROF = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
    YDVARS%U%T1(JROF,JLEV)=YDVARS%U%T1(JROF,JLEV)+YDCPG_SL2%USI(JROF,JLEV)
    YDVARS%V%T1(JROF,JLEV)=YDVARS%V%T1(JROF,JLEV)+YDCPG_SL2%VSI(JROF,JLEV)
    YDVARS%T%T1(JROF,JLEV)=YDVARS%T%T1(JROF,JLEV)+YDCPG_SL2%TSI(JROF,JLEV)
  ENDDO
ENDDO

IF(YDDYNA%LNHDYN) THEN
  DO JLEV = 1, NFLEVG
    DO JROF = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      YDVARS%SVD%T1(JROF,JLEV)=YDVARS%SVD%T1(JROF,JLEV)+YDCPG_SL2%VDSI(JROF,JLEV)
      !!!! LNHEE ................................................--
      YDVARS%SPD%T1(JROF,JLEV)=YDVARS%SPD%T1(JROF,JLEV)+YDCPG_SL2%PDSI(JROF,JLEV)
      !------------------------------------------------------------
    ENDDO
  ENDDO
ENDIF

IF (YDDYNA%LNHQE_SOLVER_QE) THEN
  YDVARS%SPD%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=0.0_JPRB
ENDIF

DO JROF = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
  YDVARS%SP%T1(JROF)=YDVARS%SP%T1(JROF)+YDCPG_SL2%SPSI(JROF)
ENDDO

!$ACDC }

IF (YDDYNA%LSPRT) THEN
  CALL  GPRCP_EXPL (YDMODEL%YRCST, YDCPG_BNDS, YDCPG_OPTS, PR=ZR1, YDVARS=YDVARS, KGFLTYP=1) 
  ZRD_R = 1.0_JPRB/YDMODEL%YRCST%RD

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn {

  DO JLEV = 1, NFLEVG
    DO JROF = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      YDVARS%T%T1(JROF,JLEV)=ZR1(JROF,JLEV)*YDVARS%T%T1(JROF,JLEV)*ZRD_R
    ENDDO
  ENDDO

!$ACDC }

ENDIF

!*    2.6  DIVIDE U AND V BY MAP FACTOR
!          ----------------------------

ZEPS=100.0_JPRB*TINY(1.0_JPRB)
LLSTR=(ABS(RSTRET-1.0_JPRB)>ZEPS)

IF(LLSTR) THEN

!$ACDC PARALLEL, TARGET=OpenMP/OpenMPSingleColumn/OpenACCSingleColumn, NAME=STR {

  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      YDVARS%U%T1(JROF,JLEV)=YDVARS%U%T1(JROF,JLEV)/YDVARS%GEOMETRY%GM%P(JROF)
      YDVARS%V%T1(JROF,JLEV)=YDVARS%V%T1(JROF,JLEV)/YDVARS%GEOMETRY%GM%P(JROF)
    ENDDO
  ENDDO

!$ACDC }

ENDIF

!*    2.7  DDH
!          ---

IF (YDDYNA%LSLAG.AND.LRSLDDH.AND.(.NOT.LDCONFX)) THEN

!$ACDC ABORT {

  CALL CPDYDDHLAG (YDGEOMETRY, YDCPG_DDH_TNDHD, YDMODEL%YRML_DIAG, YDMODEL%YRML_GCONF, YDDYNA%LNHDYN, &
                 & YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_BNDS%KSTGLO, NCURRENT_ITER, YDDDH, YDTDDH)

!$ACDC }

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('CPGLAG',1,ZHOOK_HANDLE)

END SUBROUTINE CPGLAG

